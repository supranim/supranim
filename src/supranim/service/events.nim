# Supranim - A fast MVC web framework
# for building web apps & microservices in Nim.
#
#   (c) 2025 MIT License | Made by Humans from OpenPeeps
#   https://supranim.com | https://github.com/supranim

import pkg/supranim/core/servicemanager

initService EventManager[Singleton]:
  ## A thread-based web-service for handling
  ## events in the web application.
  backend do:
    import std/[os, tables, strutils, json, options]
    import pkg/supranim/core/paths
    import pkg/threading/once

    export json

    type
      JsonString* = string
      EventCallback* = proc (args: Option[seq[JsonString]] = none(seq[JsonString])) {.nimcall, gcsafe.}
        ## The listener callback

      EventManager = object
        registeredEvents: Table[string, seq[EventCallback]]

  client do:
    proc event*(): ptr EventManager {.inline.} =
      ## Returns the Singleton instance of the EventManager
      getEventManagerInstance() # auto generated by the service manager

    proc registerListener*(eventName: string, callback: EventCallback) =
      ## Registers a listener for a specific event
      let e: ptr EventManager = event()
      if e != nil:
        if not e[].registeredEvents.hasKey(eventName):
          e[].registeredEvents[eventName] = newSeq[EventCallback]()
        e[].registeredEvents[eventName].add(callback)

    proc emitter*(eventName: string, args: Option[seq[JsonString]] = none(seq[JsonString])) =
      ## Emits an event with the given name and arguments
      let e: ptr EventManager = event()
      if e != nil:
        if e[].registeredEvents.hasKey(eventName):
          for handle in e[].registeredEvents[eventName]:
            if handle != nil:
              try:
                handle(args) # call the listener with the provided arguments
              except Exception as e:
                echo "Error in event handler for ", eventName, ": ", e.msg

    macro listener*(eventName: static string; handle: untyped): untyped =
      ## This macro is used to register an event listener
      ## for a specific event. The `eventName` is the name
      ## of the event, and `description` is an optional
      ## description of the event.
      result = newCall(
        ident"registerListener",
        newLit(eventName),
        newProc(
          params = [
            newEmptyNode(),
            nnkIdentDefs.newTree(
              newIdentNode("args"),
              nnkBracketExpr.newTree(
                newIdentNode("Option"),
                nnkBracketExpr.newTree(
                  newIdentNode("seq"),
                  newIdentNode("JsonString")
                )
              ),
              nnkCall.newTree(
                newIdentNode("none"),
                nnkBracketExpr.newTree(
                  newIdentNode("seq"),
                  newIdentNode("JsonString")
                )
              )
            )
          ],
          body = handle,
          pragmas = nnkPragma.newTree(
            ident"nimcall",
            ident"thread",
          )
        )
      )
